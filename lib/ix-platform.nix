# Target platform applied to every image.
#
# All images run on EPYC Gen 5 (Turin, Zen 5). Setting hostPlatform.gcc.arch
# propagates -march=znver5 -mtune=znver5 to every package in the closure.
# No binary cache hits: everything builds from source.
{ config, lib, ... }:
{
  options.ix.networking = {
    eastWest = {
      hostName = lib.mkOption {
        type = lib.types.str;
        default = config.networking.hostName;
      };
      firewall.allowedTCPPorts = lib.mkOption {
        type = lib.types.listOf lib.types.port;
        default = [ ];
      };
    };

    northSouth.firewall = {
      allowedTCPPorts = lib.mkOption {
        type = lib.types.listOf lib.types.port;
        default = [ ];
      };
      allowedUDPPorts = lib.mkOption {
        type = lib.types.listOf lib.types.port;
        default = [ ];
      };
    };
  };

  config = {
    nixpkgs.hostPlatform = {
      system = "x86_64-linux";
      # TODO: add back znver5 tuning for EPYC Gen 5
      # gcc = {
      #   arch = "znver5";
      #   tune = "znver5";
      # };
    };

    boot.isContainer = true;
    system.stateVersion = "25.05";

    # ix provisions the guest address, route, and DNS before systemd reaches
    # normal service startup. Leaving NixOS DHCP enabled makes dhcpcd wait for a
    # lease that will never arrive, which keeps network-online.target pending
    # and blocks services such as minecraft.
    networking.useDHCP = false;

    # Port exposure is controlled by ix VM/L7 networking, not by an in-guest
    # nftables firewall. The current guest kernel does not provide nft support,
    # so the default NixOS firewall unit fails during boot.
    networking.firewall.enable = false;
  };
}
