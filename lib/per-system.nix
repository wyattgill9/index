# Per-system flake outputs (packages / apps / checks / formatter).
#
# Kept out of flake.nix so the flake top-level can read as a manifest of
# inputs and output categories. All composition logic for apps, image preset
# wrappers, and lint plumbing lives here.
{
  system,
  ix,
  nixpkgs,
  paths,
  rust-overlay,
}:
let
  inherit (nixpkgs) lib;
  pkgs = import nixpkgs {
    inherit system;
    overlays = [ rust-overlay.overlays.default ];
  };
  fs = lib.fileset;

  mkApp = program: description: {
    type = "app";
    program = lib.getExe program;
    meta = { inherit description; };
  };

  pythonWithPydantic = pkgs.python3.withPackages (ps: [ ps.pydantic ]);

  lint = ix.writeNushellApplication pkgs {
    name = "lint";
    runtimeInputs = [
      pkgs.ast-grep
      pkgs.deadnix
      pkgs.fd
      pkgs.nixfmt
      pkgs.statix
    ];
    text = ''
      def main [] {
        let nix_files = (fd --extension nix | lines)

        print "nixfmt"
        nixfmt --check ...$nix_files

        print "statix"
        statix check .

        print "deadnix"
        deadnix --fail --no-lambda-pattern-names .

        print "ast-grep"
        ast-grep scan --error .
      }
    '';
  };

  updateMods = ix.writePythonApplication pkgs {
    name = "update-mods";
    src = paths.tools.updateMods;
    typeCheckingMode = "standard";
  };

  ixFleet = ix.writePythonApplication pkgs {
    name = "ix-fleet";
    src = paths.tools.ixFleet;
    python = pythonWithPydantic;
    typeCheckingMode = "standard";
  };

  benchFilesystem = import paths.bench.filesystem { inherit ix pkgs; };

  claudeCodeDemo = import paths.imagePresets.claudeCodeDemo {
    ix = {
      lib = ix;
    };
    hostSystem = system;
  };
  claudeCodeDemoImages = lib.mapAttrs' (
    name: package: lib.nameValuePair "claude-code-demo-${name}-image" package
  ) claudeCodeDemo.packages;
  mkDemoVmUp =
    vm:
    ix.writeNushellApplication pkgs {
      name = "claude-code-demo-${vm}-up";
      runtimeInputs = [ claudeCodeDemo.up ];
      text = ''
        def --wrapped main [...args] {
          exec ix-fleet-up --on ${vm} ...$args
        }
      '';
    };
  claudeCodeDemoLinuxUp = mkDemoVmUp "linux";
  claudeCodeDemoMinecraftUp = mkDemoVmUp "minecraft";

  repoPackages = ix.packageSetFor pkgs;

  rustPackageTests =
    let
      rustPackages = lib.getAttrs [
        "minecraft-nbt"
        "minecraft-sync-managed"
        "nix-cargo-unit"
        "oci-image-builder"
      ] repoPackages;
    in
    lib.concatMapAttrs (
      packageName: package:
      lib.mapAttrs' (testName: test: lib.nameValuePair "rust-${packageName}-${testName}" test) (
        package.passthru.tests or { }
      )
    ) rustPackages;

  lintSource = fs.toSource {
    inherit (paths) root;
    fileset = fs.gitTracked paths.root;
  };

  tests = import paths.tests { inherit nixpkgs ix; };
in
{
  packages =
    (ix.discoverImages {
      root = paths.images;
      inherit (tests) imageTests;
    })
    // claudeCodeDemo.systemPackages
    // claudeCodeDemoImages
    // {
      claude-code-demo-command = claudeCodeDemo.command;
      claude-code-demo-diff = claudeCodeDemo.diff;
      claude-code-demo-plan = claudeCodeDemo.planCommand;
      claude-code-demo-replace = claudeCodeDemo.replace;
      claude-code-demo-switch = claudeCodeDemo.switch;
      claude-code-demo-up = claudeCodeDemo.up;
      claude-code-demo-linux-up = claudeCodeDemoLinuxUp;
      claude-code-demo-minecraft-up = claudeCodeDemoMinecraftUp;
      inherit (repoPackages)
        hyperion
        minecraft-nbt
        minecraft-sync-managed
        llm-clippy
        nix-cargo-unit
        oci-image-builder
        python-mcp-server
        ;
      minestom-hello-server-jar = repoPackages.minestom.helloServerJar;
    }
    // lib.optionalAttrs (repoPackages ? ix) {
      inherit (repoPackages) ix;
    }
    // lib.optionalAttrs (system == ix.system) {
      inherit (repoPackages) tonbo-artifacts;
    };

  apps = {
    lint = mkApp lint "Run all Nix formatting and lint checks";
    bench-filesystem = mkApp benchFilesystem "Benchmark file-system behavior from inside an ix VM";
    update-mods = mkApp updateMods "Regenerate Minecraft mod catalogs";
    ix-fleet = mkApp ixFleet "Render ix fleet plans and commands";
    nix-cargo-unit = mkApp repoPackages.nix-cargo-unit "Render Cargo unit graphs as Nix derivations";
    python-mcp-server = mkApp repoPackages.python-mcp-server "Run a Python MCP server";
    claude-code-demo-diff = mkApp claudeCodeDemo.diff "Diff the Claude Code demo fleet against live VMs";
    claude-code-demo-plan = mkApp claudeCodeDemo.planCommand "Render the Claude Code demo fleet plan";
    claude-code-demo-replace = mkApp claudeCodeDemo.replace "Build replacement images for the Claude Code demo fleet";
    claude-code-demo-up = mkApp claudeCodeDemo.up "Build and upload demo OCI images, then create or start VMs from them";
    claude-code-demo-linux-up = mkApp claudeCodeDemoLinuxUp "Build and upload the Claude Code demo Linux image, then create or start only the Linux VM";
    claude-code-demo-minecraft-up = mkApp claudeCodeDemoMinecraftUp "Build and upload the Claude Code demo Minecraft image, then create or start only the Minecraft VM";
    claude-code-demo-switch = mkApp claudeCodeDemo.switch "Switch the Claude Code demo fleet";
  };

  checks =
    lib.optionalAttrs (system == ix.system) {
      inherit (tests) eval;
      cargo-unit-real-workspaces = tests.cargoUnitRealWorkspaces;
      lint = pkgs.runCommand "ix-images-lint" { nativeBuildInputs = [ pkgs.coreutils ]; } ''
        cp -R ${lintSource} source
        chmod -R u+w source
        cd source
        ${lib.getExe lint}
        mkdir -p "$out"
      '';
    }
    // lib.optionalAttrs (system == ix.system) rustPackageTests;

  formatter = pkgs.nixfmt;
}
